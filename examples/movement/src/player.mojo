from create import *


@fieldwise_init
struct Player:
    # All rates are per second, integrated with ctx.time.delta so the feel is
    # the same at any frame rate. y grows upward, so gravity is negative and a
    # jump is positive.
    comptime GRAVITY: Float64 = -5400.0  # units/s^2
    comptime JUMP_FORCE: Float64 = 1800.0  # units/s
    comptime JUMP_HOLD_FORCE: Float64 = 1080.0  # units/s^2
    comptime SPEED: Float64 = 840.0  # units/s

    var x: Float64
    var y: Float64
    var width: Float64
    var height: Float64
    var vel_y: Float64
    var on_ground: Bool
    var jumps_left: Int

    def update(mut self, mut ctx: Context, input: Input):
        var dt = ctx.time.delta

        if input.is_key_down("a"):
            self.x -= self.SPEED * dt
        if input.is_key_down("d"):
            self.x += self.SPEED * dt

        if input.just_pressed("w") and self.jumps_left > 0:
            self.vel_y = self.JUMP_FORCE
            self.jumps_left -= 1

        if input.is_key_down("w") and self.vel_y > 0:
            self.vel_y += self.JUMP_HOLD_FORCE * dt

        self.vel_y += self.GRAVITY * dt
        self.y += self.vel_y * dt

        var half_w = self.width / 2
        var half_h = self.height / 2

        if self.x - half_w < ctx.left():
            self.x = ctx.left() + half_w
        if self.x + half_w > ctx.right():
            self.x = ctx.right() - half_w

        # Ceiling: moving up and past the top edge.
        if self.y + half_h > ctx.top():
            self.y = ctx.top() - half_h
            if self.vel_y > 0:
                self.vel_y = 0.0

        if self.y - half_h <= ctx.bottom():
            self.y = ctx.bottom() + half_h
            self.vel_y = 0.0
            self.on_ground = True
            self.jumps_left = 2
        else:
            self.on_ground = False

    def draw(self, mut canvas: Canvas) raises:
        # Scoped, because this is a callee: without the guard the caller's
        # next draw would silently inherit this fill and no_stroke.
        with canvas.style():
            canvas.fill(Color(220, 80, 80))
            canvas.no_stroke()
            canvas.rectangle(self.x, self.y, self.width, self.height)
